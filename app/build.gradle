plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
    id 'org.jlleitschuh.gradle.ktlint-idea' version '10.2.0'
    id 'io.gitlab.arturbosch.detekt' version '1.18.1'
    id 'com.diffplug.spotless' version '6.18.0'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply plugin: 'io.gitlab.arturbosch.detekt'
apply plugin: "org.jlleitschuh.gradle.ktlint-idea"
apply plugin: 'com.diffplug.spotless'
apply plugin: 'kotlin-android'

detekt {
    toolVersion = "1.18.1"
    basePath = projectDir
    reports {
        html { enabled = true }
        txt { enabled = false }
        xml { enabled = false }
    }

}

spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '**/*.gradle', '**/*.md', '**/.gitignore'

        // define the steps to apply to those files
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }

    kotlin {
        target '**/*.kt'
        ktlint()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

android {
    namespace 'com.example.misw4203moviles2023'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.misw4203moviles2023"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        warningsAsErrors true
        ignoreWarnings true
        lintConfig file("lint.xml")
    }

    signingConfigs {
        release {
            storeFile rootProject.file(localProperties['RELEASE_STORE_FILE'])
            storePassword localProperties['RELEASE_STORE_PASSWORD']
            keyAlias localProperties['RELEASE_KEY_ALIAS']
            keyPassword localProperties['RELEASE_KEY_PASSWORD']

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
            all{
                jvmArgs '-noverify'
            }
        }
        animationsDisabled = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'


    // Fragment
    implementation "androidx.fragment:fragment-ktx:1.5.7"
    // Activity
    implementation "androidx.activity:activity-ktx:1.7.1"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    //Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //dagger hilt
    implementation "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    def nav_version = "2.5.3"
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Feature module Support
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Jetpack Compose Integration
    implementation "androidx.navigation:navigation-compose:$nav_version"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'


    // Testing
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "androidx.test:core-ktx:1.4.0"

    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation "androidx.test:core-ktx:1.4.0"

    // Optional -- Robolectric environment
    testImplementation 'org.robolectric:robolectric:4.10.2'

    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:5.3.1'
    // Optional -- mockito-kotlin
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'

    // Testing Navigation
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"

    // Testing Suspense Functions
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    // Testing Fragments
    debugImplementation "androidx.fragment:fragment-testing:1.6.0-alpha04" // 1.6.0-alpha04 fixes 1.5.0 bug Task :app:connectedDebugAndroidTest sitting forever on E2E tests https://stackoverflow.com/a/74955089

    // Testing LiveData
    testImplementation "androidx.arch.core:core-testing:2.2.0"

    //E2E
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'


    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.0'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.18.1")
}

task installLocalGitHook(type: Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

build.dependsOn installLocalGitHook
